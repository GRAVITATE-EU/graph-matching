PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX xlink: <http://www.w3.org/1999/xlink/>
PREFIX xsi: <http://www.w3.org/2001/XMLSchema-instance/>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX rso: <http://www.researchspace.org/ontology/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX crmdig: <http://www.ics.forth.gr/isl/CRMext/CRMdig.rdfs/>
PREFIX rdfs:<http://www.w3.org/2000/01/rdf-schema#> 
PREFIX rdf:<http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX ann:<http://gravitate.org/id/>

CONSTRUCT
{
	?artifact crm:P32_used_general_technique ?prod_technique .
	?prod_technique skos:broader ?prod_technique1 .
	?prod_technique1 skos:broader ?prod_technique2 .
	?prod_technique2 skos:broader ?prod_technique3 .
	?artifact crm:P108i_was_produced_by_falls_within ?time_concept .
	?artifact crm:P108i_was_produced_by_begin_of_the_begin ?time_start .
	?artifact crm:P108i_was_produced_by_end_of_the_end ?time_end .
	?artifact crm:P108i_was_produced_by_place ?prod_location .
}
WHERE
{
	OPTIONAL
	{
		?artifact rdf:type crm:E22_Man-Made_Object .
		?artifact crm:P108i_was_produced_by ?production_event .
		OPTIONAL
		{
			?production_event crm:P9_consists_of / crm:P32_used_general_technique ?prod_technique .
			OPTIONAL
			{
				?prod_technique skos:broader ?prod_technique1 .
				OPTIONAL
				{
					?prod_technique1 skos:broader ?prod_technique2 .
					OPTIONAL
					{
						?prod_technique2 skos:broader ?prod_technique3 .
					}
				}
			}
		} .
		OPTIONAL
		{
			?production_event crm:P9_consists_of / crm:P10_falls_within ?time_concept .
		} .
		OPTIONAL
		{
			?production_event crm:P9_consists_of / crm:P4_has_time-span ?time_span .
			?time_span crm:P82a_begin_of_the_begin ?time_start .
			?time_span crm:P82b_end_of_the_end ?time_end .
		} .
		OPTIONAL {
			?production_event crm:P9_consists_of / crm:P7_took_place_at ?prod_location .
		} .
	}
}
